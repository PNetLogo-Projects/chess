__includes [
  "game/fen_notation/load-fen-notation.nls"
  "game/fen_notation/save-fen-notation.nls"
]

to board-initialize-constants
  set board-light-color 8
  set board-dark-color 57
  set margin-color brown
  set board-patches (patches with [pxcor >= 0 and pxcor <= 7 and pycor >= 0 and pycor <= 7])
end

to render-board 
  let save-color? true
  
  ask patches [
    set marked? false
    set saved-marked? false
    
    ifelse pxcor < 0 or pxcor > 7
    [(color-patch-spot margin-color save-color?)]
    [
      ifelse (pxcor + pycor) mod 2 = 0
      [(color-patch-spot board-dark-color save-color?)]
      [(color-patch-spot board-light-color save-color?)]
    ]
  ]
end

to color-patch-spot [colour save?]
  set pcolor colour
  if save?
  [set orig-color pcolor]
end

to flip-board
  ask board-patches
  [
    ifelse pcolor = board-light-color
    [
      set pcolor board-dark-color
      set orig-color board-dark-color
    ]
    [
      set pcolor board-light-color
      set orig-color board-light-color
    ]
  ]
end

to reset-marks
  ask board-patches
  [
    set marked? false
    if pcolor != blue
    [set pcolor orig-color]
  ]
  sync-screen
end

to treset-marks
  ask board-patches
  [
    set marked? false
    if pcolor != blue
    [set pcolor orig-color]
  ]
end

to save-marks-to-saved-marked
  ask board-patches
  [set saved-marked? marked?]
end

to load-marks-from-saved-marked
  ask board-patches
  [set marked? saved-marked?]
end

to-report within-board-bounds? [x-coordinate y-coordinate]
  report x-coordinate >= 0 and x-coordinate <= 7 and y-coordinate >= 0 and y-coordinate <= 7
end