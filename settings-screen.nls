to settings-initialize-constants
  set settings-config-filename "config.ini"
end

to settings-initialize-globals
  set settings-game-clock-times (list " 1:00" " 3:00" " 5:00" "10:00" "15:00" "30:00" "60:00")
  set settings-game-clock-time-index 2
  set settings-game-clock-time-turtles []
  (load-settings-from-file "config.ini")
end

to draw-settings-screen
  cd
  cp
  ct
  initialize-constants
  initialize-globals
  
  (render-string "Settings Menu" [-3 6.5] 1 115)
  let checks-x-coordinate -3
  let checks-hidden-state? true
  let check-box-color white
  let check-color black
  (draw-menu-with-checks 
    (list checks-x-coordinate checks-hidden-state? check-box-color check-color)
    (list 
      (list "Game Clock" [-2 5] menu-left-align 1 115)
      (list "Highlight Available Moves" [-2 4] menu-left-align 1 115)
      (list "Switch View in 2 Players" [-2 3] menu-left-align 1 115)
      (list "Allow Players to Undo Moves" [-2 2] menu-left-align 1 115)))
  
  (aligned-render-string "Ok" [2.25 .5] text-center-align 1 115)
  (aligned-render-string "Cancel" [7.75 .5] text-center-align 1 115)
  
  (render-string "<" [5 5] 1 115)
  let time (item settings-game-clock-time-index settings-game-clock-times)
  (set settings-game-clock-time-turtles (temp-aligned-render-string time [6.1 5] text-center-align 1 115))
  (render-string ">" [7 5] 1 115)
  
  (load-settings-from-file settings-config-filename)
  update-settings-screen
end

to animate-settings-screen
  let user-has-confirmed? false
  let user-has-cancelled? false
  let ok-turtles (display-turtles with [xcor < 6 and ycor = .5])
  let cancel-turtles (display-turtles with [xcor > 6 and ycor = .5])
  let previous-turtle (display-turtles with [shape = "<"])
  let next-turtle (display-turtles with [shape = ">"])
  
  while [not user-has-confirmed? and not user-has-cancelled?]
  [
    let mouse-x (round mouse-xcor)
    let mouse-y (round mouse-ycor)
    
    ask previous-turtle
    [
      ifelse hovering-over-previous?
      [set color 125]
      [set color 115]
    ]
    ask next-turtle
    [
      ifelse hovering-over-next?
      [set color 125]
      [set color 115]
    ]
    if mouse-down? and hovering-over-previous?
    [
      set settings-game-clock-time-index ((settings-game-clock-time-index - 1) mod (length settings-game-clock-times))
      ask (turtle-set settings-game-clock-time-turtles)
      [die]
      let time (item settings-game-clock-time-index settings-game-clock-times)
      (set settings-game-clock-time-turtles (temp-aligned-render-string time [6.1 5] text-center-align 1 115))
      while [mouse-down?]
      []
    ]
    if mouse-down? and hovering-over-next?
    [
      set settings-game-clock-time-index ((settings-game-clock-time-index + 1) mod (length settings-game-clock-times))
      ask (turtle-set settings-game-clock-time-turtles)
      [die]
      let time (item settings-game-clock-time-index settings-game-clock-times)
      (set settings-game-clock-time-turtles (temp-aligned-render-string time [6.1 5] text-center-align 1 115))
      while [mouse-down?]
      []
    ]
    
    if mouse-down? and (mouse-x = -3 and member? mouse-y [2 3 4 5])
    [
      menu-toggle-check-box mouse-x mouse-y
      while [mouse-down?]
      []
    ]
    
    ask ok-turtles
    [
      ifelse hovering-over-ok?
      [set color 125]
      [set color 115]
    ]
    ask cancel-turtles
    [
      ifelse hovering-over-cancel?
      [set color 125]
      [set color 115]
    ]
    if mouse-down? and hovering-over-ok?
    [
      update-settings-variables
      save-settings-to-file settings-config-filename
      set user-has-confirmed? true
    ]
    if mouse-down? and hovering-over-cancel?
    [set user-has-cancelled? true]
  ]
  
  draw-splash-screen
  animate-splash-screen
end

to update-settings-screen
  let check-turtles (turtles with [shape = "check"])
  ask check-turtles with [xcor = -3 and ycor = 5]
  [set hidden? not settings-play-by-time?]
  ask check-turtles with [xcor = -3 and ycor = 4]
  [set hidden? not settings-highlight-valid-move?]
  ask check-turtles with [xcor = -3 and ycor = 3]
  [set hidden? not settings-switch-view?]
  ask check-turtles with [xcor = -3 and ycor = 2]
  [set hidden? not settings-allow-undo?]
end

to update-settings-variables
  let check-turtles (turtles with [shape = "check"])
  set settings-play-by-time? (not one-of [hidden?] of check-turtles with [xcor = -3 and ycor = 5])
  set settings-highlight-valid-move? (not one-of  [hidden?] of check-turtles with [xcor = -3 and ycor = 4])
  set settings-switch-view? (not one-of [hidden?] of check-turtles with [xcor = -3 and ycor = 3])
  set settings-allow-undo? (not one-of [hidden?] of check-turtles with [xcor = -3 and ycor = 2])
end

to write-default-config [filename]
  set settings-play-by-time? false
  set settings-game-clock-time-index 2
  set settings-highlight-valid-move? false
  set settings-switch-view? false
  set settings-allow-undo? false
  
  save-settings-to-file filename
end

to load-settings-from-file [filename]
  let result (read-from-file filename)
  let file-content (item 0 result)
  let err (item 1 result)
  
  ifelse err
  [
    write-default-config filename
    load-settings-from-file filename
  ]
  [
    set settings-play-by-time? (item 0 file-content)
    set settings-game-clock-time-index (item 1 file-content)
    set settings-highlight-valid-move? (item 2 file-content)
    set settings-switch-view? (item 3 file-content)
    set settings-allow-undo? (item 4 file-content)
  ]
end

to save-settings-to-file [filename]
  let success (write-to-file
    filename 
    (list settings-play-by-time?
      settings-game-clock-time-index
      settings-highlight-valid-move?
      settings-switch-view?
      settings-allow-undo?)
    true)
  if not success
  [user-message "There was an error writing settings to config file!"]
end

to-report hovering-over-ok?
  let ok-turtles (display-turtles with [xcor < 6 and ycor = .5])
  report (mouse-ycor >= 0 and mouse-ycor <= 1 and 
    mouse-xcor >= min [xcor] of ok-turtles and mouse-xcor <= max [xcor] of ok-turtles)
end

to-report hovering-over-cancel?
  let cancel-turtles (display-turtles with [xcor > 6 and ycor = .5])
  report (mouse-ycor >= 0 and mouse-ycor <= 1 and 
    mouse-xcor >= min [xcor] of cancel-turtles and mouse-xcor <= max [xcor] of cancel-turtles)
end

to-report hovering-over-previous?
  if not any? display-turtles with [shape = "<"]
  [report false]
  let previous-turtle (one-of display-turtles with [shape = "<"])
  let previous-xcor [xcor] of previous-turtle
  let previous-ycor [ycor] of previous-turtle
  report (mouse-xcor >= previous-xcor - .5 and mouse-xcor <= previous-xcor + .5 and
    mouse-ycor >= previous-ycor - .5 and mouse-ycor <= previous-ycor + .5)
end

to-report hovering-over-next?
  if not any? display-turtles with [shape = ">"]
  [report false]
  let next-turtle (one-of display-turtles with [shape = ">"])
  let next-xcor [xcor] of next-turtle
  let next-ycor [ycor] of next-turtle
  report (mouse-xcor >= next-xcor - .5 and mouse-xcor <= next-xcor + .5 and
    mouse-ycor >= next-ycor - .5 and mouse-ycor <= next-ycor + .5)
end